NEW 

    1000 ? "THE KINGDOM OF EUPHORIA"
    1010 ?
    1020 GOSUB 3510
    1100 REM ? STATE OF THE KINGDOM
    1110 ?
    1120 ? "YEAR ",Y/7
    1130 ? "POPULATION: " P
    1140 IF B>0 THEN ? B " BIRTHS"
    1150 IF D>0 THEN ? D " DEATHS BY NATURAL CAUSES"
    1160 IF S>0 THEN ? S " DEATHS BY STARVATION"
    1170 IF K>0 THEN ? K " WAR CASUALTIES"
    1180 IF V>0 THEN ? V " VICTIMS OF DISEASE"
    1190 IF M>0 THEN ? M " VICTIMS OF LOOTING"
    1210 LET D=D+S+K+V+M
    1220 LET P=P+B-D
    1230 IF B+D>0 THEN ? "TOTAL: " P
    1250 ? "LAND (ACRES): " A
    1260 IF T=0 THEN IF U=0 THEN GOTO 1350
    1270 IF T>0 THEN ? T " ACRES BOUGHT"
    1280 IF T<0 THEN ? -T " ACRES SOLD"
    1290 IF U<>0 THEN ? "FRUITS OF WAR: " U " ACRES"
    1310 LET A=A+T+U
    1320 LET T=0
    1330 LET U=0
    1340 ? "TOTAL: " A
    1350 ? "GRAIN (BUSHELS): " G
    1360 IF C<0 THEN GOTO 1530
    1370 IF C=0 THEN GOTO 1430
    1380 LET R=X
    1390 ? "CROP YIELD " C " AT ",
    1410 GOSUB 3220
    1420 ?
    1430 IF F>0 THEN ? F " BUSHELS USED FOR FOOD"
    1440 IF Q>0 THEN ? Q " BUSHELS PLANTED"
    1450 IF L<>0 THEN ? "LAND DEALS: " L " BUSHELS"
    1460 IF H>0 THEN ? "MERCENARY HIRE: " H
    1470 IF E>0 THEN ? E " BUSHELS LOST TO RATS"
    1480 IF Z<>0 THEN ? "FRUITS OF WAR: " Z " BUSHELS"
    1490 IF O>0 THEN ? "LOOTING LOSSES: " O " BUSHELS"
    1510 LET G=G+C-F-Q+L-H-E+Z-O
    1520 ? "TOTAL: " G
    1530 GOSUB 3310
    1600 REM NOTICE IF GAME ENDED
    1610 IF J<100 THEN GOTO 1690
    1620 ? "THE PEASANTS TIRE OF WAR AND STARVATION"
    1630 ? "YOU ARE DEPOSED."
    1640 ? "DO YOU WISH TO PLAY AGAIN",
    1660 INPUT N
    1670 IF N<>ASC(\Y) THEN END
    1680 GOTO 1000
    1690 IF P>1 THEN GOTO 1750
    1710 ? "YOU AND THE REMAINING POPULATION"
    1720 ? "RETIRE IN THE SWISS ALPS."
    1730 GOTO 1640
    1740 REM MAKE LAND DEALS
    1750 LET R=23+RND(8)
    1760 GOSUB 3250
    1770 ? "BUY AT ",
    1780 GOSUB 3220
    1790 INPUT T
    1810 IF T<0 THEN GOTO 1760
    1820 IF T=0 THEN GOTO 1860
    1830 IF R*T<=G THEN GOTO 2060
    1840 GOSUB 3270
    1850 GOTO 1760
    1860 LET R=R-1
    1880 GOSUB 3250
    1890 ? "SELL AT ",
    1910 GOSUB 3220
    1920 INPUT T
    1930 IF T<0 THEN GOTO 1880
    1940 IF T=0 THEN GOTO 2060
    1950 IF T<=A THEN GOTO 1990
    1960 ? "BUT THERE IS INSUFFICIENT LAND"
    1970 GOTO 1880
    1990 IF T<A/10 THEN GOTO 2050
    2010 LET R=R-1
    2020 ? "FOR SELLING SO MUCH YOU CAN ONLY GET "
    2030 GOSUB 3220
    2040 ?
    2050 LET T=-T
    2060 LET L=-R*T
    2100 REM DISTRIBUTE GRAIN
    2110 GOSUB 3250
    2120 ? "PLANT"
    2130 INPUT Q
    2140 IF Q<0 THEN GOTO 2110
    2150 IF Q<=A+T THEN GOTO 2180
    2160 GOSUB 3270
    2170 GOTO 2110
    2180 IF Q<=P*10 THEN GOTO 2220
    2190 ? "BUT THERE ARE INSUFFICIENT PEOPLE"
    2210 GOTO 2110
    2220 ? "HOW MANY BUSHELS DO YOU WISH TO ",
    2230 ? "USE AS FOOD",
    2240 INPUT F
    2250 IF F<0 THEN GOTO 2220
    2260 IF G+L-Q-F>=0 THEN GOTO 2310
    2270 GOSUB 3270
    2280 GOTO 2220
    2310 IF F<=40*P THEN LET S=P-F/40
    2320 LET J=J+S
    2330 LET X=5+RND(4)
    2340 IF Y/7*8-Y/49*7=Y THEN LET X=X/2-1
    2350 LET C=X*Q
    2360 IF G+L-Q-F+C>=0 THEN GOTO 2390
    2370 LET X=(32767-G-L+Q+F)/Q
    2380 GOTO 2350
    2390 IF RND(99)<25 THEN LET E=(G+L-F+C)/10
    2400 REM WAR AND PLAGUE
    2410 IF RND(99)>15 THEN GOTO 2880
    2420 LET W=25
    2430 ? "A NEARBY KINGDOM THREATENS WAR"
    2440 ? "DO YOU WISH TO MAKE A",
    2450 ? " PRE-EMPTIVE STRIKE",
    2470 INPUT N
    2480 IF N<>ASC(\Y) THEN GOTO 2520
    2490 LET W=100
    2510 LET J=J+5
    2520 ? "HOW MANY MERCENARIES WILL YOU",
    2530 ? " HIRE AT 80 BUSHELS EACH",
    2540 INPUT N
    2550 IF N<0 THEN GOTO 2520
    2560 LET D=G+L-Q-F+C-E
    2570 IF RND(99)<W THEN GOTO 2610
    2580 ? "PEACE NEGOTIATIONS SUCCEED"
    2590 GOTO 2810
    2610 IF W=25 THEN LET W=150
    2620 LET R=P-S
    2630 IF N>R/10 THEN GOTO 2660
    2640 LET R=3*W/5*N*N/R*100/R
    2650 GOTO 2680
    2660 LET R=3*W/5+N*100/R
    2680 LET K=(P-S)/2
    2690 LET U=-(A+T)/2
    2710 LET Z=-D/2
    2720 IF R>RND(99) THEN GOTO 2760
    2730 ? "YOU HAVE LOST THE WAR"
    2740 LET J=J+5000/W
    2750 GOTO 2810
    2760 ? "YOU HAVE WON THE WAR"
    2770 LET K=K/2
    2780 LET U=-U
    2790 LET Z=D/4
    2810 IF N*80<=D+Z THEN GOTO 2870
    2820 GOSUB 3280
    2830 ? "TO PAY THE MERCENARIES"
    2840 LET M=3*(P-S-K)/4
    2850 LET O=3*(R+Z)/4
    2860 GOTO 2880
    2870 LET H=N*80
    2880 LET N=P-S-K-M
    2890 IF RND(99)>4 THEN GOTO 2940
    2910 ? "THE BLACK PLAGUE STRIKES"
    2920 LET V=N/2
    2930 GOTO 2970
    2940 IF RND(99)>20 THEN GOTO 2980
    2950 ? "A POX EPIDEMIC BREAKS OUT"
    2960 LET V=N/20
    2970 LET N=N-V
    2980 LET B=(N*RND(5)+9)/100+1
    2990 LET D=(N*RND(3)+4)/100
    3010 IF Y/7*8-Y/49*7<>Y THEN GOTO 3030
    3020 ? "SEVEN YEAR LOCUSTS REDUCE CROP YIELD"
    3030 IF E>0 THEN ? "RATS INFEST YOUR SILOS"
    3040 LET Y=Y+8-(Y-Y/7*7)/6
    3050 GOTO 1110
    3200 REM UTILITY ? ROUTINES
    3220 ? R " BUSHELS/ACRE",
    3230 RETURN
    3250 ? "HOW MANY ACRES DO YOU WISH TO ",
    3260 RETURN
    3270 ? "BUT ",
    3280 ? "THERE IS INSUFFICIENT GRAIN"
    3290 RETURN
    3300 REM INITIALIZE POPULATION AND GRAIN VECTORS
    3490 RETURN
    3500 REM INITIALIZATION
    3510 LET Y=RND(6)+1
    3520 LET J=0
    3530 LET P=100
    3540 LET A=1500
    3550 LET G=5000
    3560 LET T=0
    3570 LET U=0
    3580 GOSUB 3310
    3590 LET C=-1
    3600 REM ? INSTRUCTIONS (MAY BE OMITTED)
    3610 ? "DO YOU NEED INSTRUCTIONS (Y OR N)",
    3630 INPUT N
    3640 IF N=ASC(\N) THEN RETURN
    3650 IF N=ASC(\Y) THEN GOTO 3710
    3660 ? "PLEASE TYPE Y FOR YES, N FOR NO. OK",
    3670 GOTO 3630
    3710 ? "YOU HAVE INHERITED THE THRONE TO THE MEDIEVAL"
    3720 ? "KINGDOM OF EUPHORIA, SOMEWHERE IN EUROPE."
    3730 ? "AT THIS TIME IT IS ABOUT " A " ACRES IN SIZE,"
    3740 ? "WITH " P " LOYAL PEASANTS TO SERVE YOU."
    3750 ? "IN YOUR ROYAL SILOS YOU HAVE " G " BUSHELS OF"
    3760 ? "NUTRITIOUS GRAIN WITH WHICH TO FEED THE PEOPLE"
    3770 ? "AND TRANSACT INTERNATIONAL TRADE."
    3780 ? "CLOSE BY YOUR SIDE ARE JEALOUS NEIGHBORING"
    3790 ? "KINGDOMS, BUT YOU CAN PROTECT YOURSELF BY"
    3810 ? "HIRING EVIL MERCENARIES."
    3820 ? "EACH YEAR YOUR MINISTERS WILL PRESENT YOU"
    3830 ? "WITH A SUMMARY OF YOUR CURRENT STATUS,"
    3840 ? "AND THEN ASK YOU FOR DECISIONS ON WHAT"
    3850 ? "TO DO FOR THE NEXT YEAR. PLEASE TYPE Y OR N"
    3860 ? "FOR YES OR NO, OR A WHOLE NUMBER FOR"
    3870 ? "NUMERIC ANSWERS. IF YOU CHANGE YOUR MIND"
    3880 ? "BEFORE HITTING RETURN, HOLD THE 'CTRL' KEY"
    3890 ? "DOWN WHILE TYPING THE LETTER 'X', THEN"
    3910 ? "RETYPE YOUR ANSWER ON THE NEXT LINE."
    3920 ? "ARE YOU READY, YOUR HIGHNESS",
    3940 INPUT N
    3950 IF N<>ASC(\Y) THEN GOTO 3920
    3980 RETURN
    3990 END

 
